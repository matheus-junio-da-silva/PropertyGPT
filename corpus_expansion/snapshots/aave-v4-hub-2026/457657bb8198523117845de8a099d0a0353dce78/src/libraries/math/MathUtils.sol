// SPDX-License-Identifier: MIT
pragma solidity ^0.8.0;

import {WadRayMath} from './WadRayMath.sol';

/**
 * @title MathUtils library
 * @author Aave
 * @notice Provides functions to perform linear and compounded interest calculations
 */
library MathUtils {
  using WadRayMath for uint256;

  /// @dev Ignoring leap years
  uint256 internal constant SECONDS_PER_YEAR = 365 days;

  /**
   * @dev Function to calculate the interest accumulated using a linear interest rate formula.
   * @param rate The interest rate, in ray.
   * @param lastUpdateTimestamp The timestamp of the last update of the interest.
   * @return The interest rate linearly accumulated during the timeDelta, in ray.
   */
  function calculateLinearInterest(
    uint256 rate,
    uint40 lastUpdateTimestamp
  ) internal view returns (uint256) {
    //solium-disable-next-line
    uint256 result = rate * (block.timestamp - uint256(lastUpdateTimestamp));
    unchecked {
      result = result / SECONDS_PER_YEAR;
    }

    return WadRayMath.RAY + result;
  }

  /**
   * @dev Function to calculate the interest using a compounded interest rate formula.
   * To avoid expensive exponentiation, the calculation is performed using a binomial approximation:
   *
   *  (1+x)^n = 1+n*x+[n/2*(n-1)]*x^2+[n/6*(n-1)*(n-2)*x^3...
   *
   * The approximation slightly underpays liquidity providers and undercharges borrowers, with the advantage of great
   * gas cost reductions. The whitepaper contains reference to the approximation and a table showing the margin of
   * error per different time periods
   *
   * @param rate The interest rate, in ray.
   * @param lastUpdateTimestamp The timestamp of the last update of the interest.
   * @return The interest rate compounded during the timeDelta, in ray.
   */
  function calculateCompoundedInterest(
    uint256 rate,
    uint40 lastUpdateTimestamp,
    uint256 currentTimestamp
  ) internal pure returns (uint256) {
    //solium-disable-next-line
    uint256 exp = currentTimestamp - uint256(lastUpdateTimestamp);

    if (exp == 0) {
      return WadRayMath.RAY;
    }

    uint256 expMinusOne;
    uint256 expMinusTwo;
    uint256 basePowerTwo;
    uint256 basePowerThree;
    unchecked {
      expMinusOne = exp - 1;

      expMinusTwo = exp > 2 ? exp - 2 : 0;

      basePowerTwo = rate.rayMul(rate) / (SECONDS_PER_YEAR * SECONDS_PER_YEAR);
      basePowerThree = basePowerTwo.rayMul(rate) / SECONDS_PER_YEAR;
    }

    uint256 secondTerm = exp * expMinusOne * basePowerTwo;
    unchecked {
      secondTerm /= 2;
    }
    uint256 thirdTerm = exp * expMinusOne * expMinusTwo * basePowerThree;
    unchecked {
      thirdTerm /= 6;
    }

    return WadRayMath.RAY + (rate * exp) / SECONDS_PER_YEAR + secondTerm + thirdTerm;
  }

  /**
   * @dev Calculates the compounded interest between the timestamp of the last update and the current block timestamp.
   * @param rate The interest rate (in ray).
   * @param lastUpdateTimestamp The timestamp from which the interest accumulation needs to be calculated.
   * @return The interest rate compounded between lastUpdateTimestamp and current block timestamp, in ray.
   */
  function calculateCompoundedInterest(
    uint256 rate,
    uint40 lastUpdateTimestamp
  ) internal view returns (uint256) {
    return calculateCompoundedInterest(rate, lastUpdateTimestamp, block.timestamp);
  }

  /**
   * @notice Calculates the new weighted average given a current weighted average, the sum of the weights subtracted with a new value, weight.
   * @dev Add precision to weighted average & new value before calling this method.
   * @param currentWeightedAvg The base weighted average.
   * @param currentSumWeights The base sum of weights.
   * @param newValue The new value to add or subtract.
   * @param newValueWeight The weight of the new value.
   * @return newWeightedAvg The weighted average after the operation.
   * @return newSumWeights The sum of weights after operation, cannot be less than 0.
   */
  function addToWeightedAverage(
    uint256 currentWeightedAvg,
    uint256 currentSumWeights,
    uint256 newValue,
    uint256 newValueWeight
  ) internal pure returns (uint256, uint256) {
    // newWeightedAvg, newSumWeights

    if (newValueWeight == 0) {
      return (currentWeightedAvg, currentSumWeights);
    }
    if (currentSumWeights == 0) {
      return (newValue, newValueWeight);
    }

    uint256 newSumWeights = currentSumWeights + newValueWeight;
    uint256 newWeightedAvg = ((currentWeightedAvg * currentSumWeights) +
      (newValue * newValueWeight)) / newSumWeights; // newSumWeights cannot be zero when execution reaches here

    return (newWeightedAvg, newSumWeights);
  }

  /**
   * @notice Calculates the new weighted average given a current weighted average, the sum of the weights added with a new value, weight.
   * @dev Add precision to weighted average & new value before calling this method.
   * @param currentWeightedAvg The base weighted average.
   * @param currentSumWeights The base sum of weights.
   * @param newValue The new value to add or subtract.
   * @param newValueWeight The weight of the new value.
   * @return newWeightedAvg The weighted average after the operation.
   * @return newSumWeights The sum of weights after operation, cannot be less than 0.
   * @dev Reverts when newValueWeight is greater than currentSumWeights.
   * @dev Reverts when the newWeightedValue (weight * value) is greater than currentWeightedSum (currentSumWeights * currentWeightedAvg).
   */
  function subtractFromWeightedAverage(
    uint256 currentWeightedAvg,
    uint256 currentSumWeights,
    uint256 newValue,
    uint256 newValueWeight
  ) internal pure returns (uint256, uint256) {
    // newWeightedAvg, newSumWeights
    if (newValueWeight == 0) return (currentWeightedAvg, currentSumWeights);

    if (currentSumWeights == newValueWeight) return (0, 0); // no change
    if (currentSumWeights < newValueWeight) revert();

    uint256 newWeightedValue = newValue * newValueWeight;
    uint256 currentWeightedSum = currentWeightedAvg * currentSumWeights;

    if (currentWeightedSum < newWeightedValue) revert();

    uint256 newSumWeights = currentSumWeights - newValueWeight;
    uint256 newWeightedAvg = (currentWeightedSum - newWeightedValue) / newSumWeights;

    return (newWeightedAvg, newSumWeights);
  }

  /**
   * @notice Returns the minimum of two values.
   * @param a The first value to compare.
   * @param b The second value to compare.
   * @return result The minimum of the two values.
   */
  function min(uint256 a, uint256 b) internal pure returns (uint256 result) {
    assembly {
      result := xor(b, mul(xor(a, b), lt(a, b)))
    }
  }
}
