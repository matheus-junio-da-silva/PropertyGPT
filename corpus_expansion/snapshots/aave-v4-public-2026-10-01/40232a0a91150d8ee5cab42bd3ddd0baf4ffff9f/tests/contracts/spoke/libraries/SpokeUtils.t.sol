// SPDX-License-Identifier: MIT
pragma solidity ^0.8.0;

import 'tests/setup/Base.t.sol';

contract SpokeUtilsTest is Base {
  SpokeUtilsWrapper internal w;

  ISpoke.Reserve internal reserve0;
  ISpoke.Reserve internal reserve1;
  ISpoke.Reserve internal reserve2;

  function setUp() public virtual override {
    super.setUp();

    w = new SpokeUtilsWrapper();

    reserve0 = ISpoke.Reserve({
      underlying: address(tokenList.usdx),
      hub: IHubBase(address(1)),
      assetId: 1,
      decimals: 6,
      collateralRisk: 10_00,
      flags: ReserveFlagsMap.create(false, false, true, true),
      dynamicConfigKey: 0
    });

    reserve1 = ISpoke.Reserve({
      underlying: address(tokenList.weth),
      hub: IHubBase(address(2)),
      assetId: 2,
      decimals: 18,
      collateralRisk: 15_00,
      flags: ReserveFlagsMap.create(false, false, true, true),
      dynamicConfigKey: 3
    });

    reserve2 = ISpoke.Reserve({
      underlying: address(tokenList.dai),
      hub: IHubBase(address(0)),
      assetId: 3,
      decimals: 18,
      collateralRisk: 20_00,
      flags: ReserveFlagsMap.create(false, false, true, true),
      dynamicConfigKey: 1
    });
  }

  function _populateReserves() public {
    w.setReserve(0, reserve0);
    w.setReserve(1, reserve1);
    w.setReserve(2, reserve2);
  }

  function test_get_revertsWith_ReserveNotListed() public {
    vm.expectRevert(ISpoke.ReserveNotListed.selector);
    w.get(0);
    _populateReserves();
    vm.expectRevert(ISpoke.ReserveNotListed.selector);
    w.get(2);
  }

  function test_get() public {
    _populateReserves();
    assertEq(w.get(0), reserve0);
    assertEq(w.get(1), reserve1);
  }

  // Reverts if asset uses more than 18 decimals.
  function test_toValue_revertsWith_ArithmeticUnderflow() public {
    vm.expectRevert(stdError.arithmeticError);
    w.toValue(1, 19, 1e8);
  }

  // Reverts if multiplication overflows.
  function test_toValue_revertsWith_ArithmeticOverflow() public {
    vm.expectRevert(stdError.arithmeticError);
    w.toValue(1e50, 6, 1e16);
  }

  function test_toValue() public view {
    assertEq(w.toValue(4.2e6, 6, 200e8), 840e26);
  }

  function test_fuzz_toValue(uint256 amount, uint256 decimals, uint256 price) public view {
    amount = bound(amount, 0, MAX_SUPPLY_AMOUNT);
    decimals = bound(decimals, MIN_ALLOWED_UNDERLYING_DECIMALS, MAX_ALLOWED_UNDERLYING_DECIMALS);
    price = bound(price, 0, MAX_ASSET_PRICE);
    assertEq(w.toValue(amount, decimals, price), amount * price * (10 ** (18 - decimals)));
  }
}
